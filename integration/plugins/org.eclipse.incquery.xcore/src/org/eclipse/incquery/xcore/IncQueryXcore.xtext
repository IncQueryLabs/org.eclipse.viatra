/*******************************************************************************
 * Copyright (c) 2010-2012, Tamas Szabo, Istvan Rath and Daniel Varro
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *
 * Contributors:
 *   Tamas Szabo (itemis AG) - initial API and implementation
 *******************************************************************************/
grammar org.eclipse.incquery.xcore.IncQueryXcore with org.eclipse.emf.ecore.xcore.Xcore

import "http://www.eclipse.org/emf/2011/Xcore" as base
import "http://www.eclipse.org/incquery/patternlanguage/PatternLanguage" as patternLanguage
import "http://www.eclipse.org/incquery/IncQueryXcoreLanguage"

XIncQueryPackage:
	{XIncQueryPackage}
	(annotations+=XAnnotation)*
	'package'
	name = QualifiedName
	(importDirectives += XImportDirective)*
	(importedIncQueries += XIncQueryImport)*
	(annotationDirectives += XAnnotationDirective)*
	(classifiers += XClassifier)*
;

XIncQueryImport:
	'import-incqueries' (importedPatternModel=[patternLanguage::PatternModel|STRING]) (';')?
;

XMember returns base::XMember:
	XOperation |
	XAttribute |
	XReference | 
	XIncQueryDerivedFeature
;

XIncQueryDerivedFeature:
    {XIncQueryDerivedFeature}
    'incquery-derived'
    (reference?='refers')?
    type=XGenericType multiplicity=XMultiplicity?
    name=ID
    'spec' (pattern=[patternLanguage::Pattern|QualifiedName])
    (';')?
;