/*******************************************************************************
 * Copyright (c) 2010-2012, Zoltan Ujhelyi, Istvan Rath and Daniel Varro
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *
 * Contributors:
 *   Zoltan Ujhelyi - initial API and implementation
 *******************************************************************************/
grammar org.eclipse.viatra.query.patternlanguage.PatternLanguage with org.eclipse.xtext.xbase.Xbase

generate patternLanguage "http://www.eclipse.org/viatra/query/patternlanguage/PatternLanguage"
import "http://www.eclipse.org/xtext/xbase/Xbase" as xbase
import "http://www.eclipse.org/xtext/common/JavaVMTypes" as types
import "http://www.eclipse.org/emf/2002/Ecore" as ecore

PatternModel:
	('package' packageName=QualifiedName)?
	patterns+=Pattern*;

Pattern:
	annotations+=Annotation*
	modifiers=Modifiers
	'pattern'
	name=ID
	'('
	(parameters+=Parameter (',' parameters+=Parameter)*)?
	')'
	'='?
	bodies+=PatternBody ('or' bodies+=PatternBody)*;

Annotation:
	'@'
	name=ID
	('(' parameters+=AnnotationParameter (',' parameters+=AnnotationParameter)* ')')?;

AnnotationParameter:
	name=ID '=' value=AnnotationValueReference;

Modifiers:
    {Modifiers}(
//injective?='injective' &
	private?='private'? &
	execution=ExecutionTypeModifier?
	);

enum ExecutionType:
    unspecified | search | incremental ;

enum ExecutionTypeModifier returns ExecutionType:
    search | incremental
;

Variable returns Expression:
	{Variable}
	name=ID
	(':' type=Type)?;

ParameterRef returns Variable:
	{ParameterRef}
	name=ID
	referredParam = [Variable]
	(':' type=Type)?
;

enum ParameterDirection:
    inout | in | out
;

enum ParameterDirectionModifier returns ParameterDirection:
    in | out
;

Parameter returns Variable:
    {Parameter}
    direction=ParameterDirectionModifier?
	name=ID
	(':' type=Type | ':' type=JavaType)?
;

LocalVariable returns Variable:
	{LocalVariable}
	name=ID
	(':' type=Type)?
;

Expression returns Expression:
    VariableReference | ValueReference
;

VariableReference returns VariableReference:
	aggregator?='#'? var = ID |
	aggregator?='#';


//These types are not meant to be written directly, just in the desc languages	
Type returns Type:
	{EntityType}
	typename=ID;

JavaType returns Type:
    {JavaType}
    typename='java' classRef=[types::JvmDeclaredType|QualifiedName]
;

//These types are not meant to be written directly, just in the desc languages
RefType returns Type:
	{RelationType}
	typename=ID;

PatternBody returns PatternBody:
	{PatternBody}
	(name=ID)?
	'{'
	((constraints+=Constraint ';'))*
	'}';

Constraint returns Constraint:
    CheckConstraint | TypeCheckConstraint |
	CompareConstraint | PatternCompositionConstraint | PathExpressionConstraint;

TypeCheckConstraint returns Constraint:
    {TypeCheckConstraint} type = JavaType  '(' var = VariableReference ')'
;

PatternCompositionConstraint returns Constraint:
	{PatternCompositionConstraint} negative?='neg'? call=PatternCall;
	
	//'count' 'find' patternRef=[Pattern] '(' (parameters+=ValueReference (','
	//parameters+=ValueReference)*)? ')';
PatternCall:
	'find' patternRef=[Pattern|QualifiedName] transitive?='+'? '(' (parameters+=ValueReference (',' parameters+=ValueReference)*)? ')';

CompareConstraint returns Constraint:
	{CompareConstraint} leftOperand = ValueReference feature=CompareFeature rightOperand = ValueReference 
;

enum CompareFeature:
	equality = '==' | inequality = '!='
;

CheckConstraint returns Constraint:
	{CheckConstraint} 'check' '(' expression=XExpression ')';

PathExpressionConstraint returns Constraint:
	{PathExpressionConstraint} head=PathExpressionHead;

PathExpressionHead returns PathExpressionElement:
	{PathExpressionHead} type=Type '.' tail=PathExpressionTail '(' src=VariableReference ',' dst=ValueReference ')';

PathExpressionTail returns PathExpressionElement:
	{PathExpressionTail} type=RefType ('.' tail=PathExpressionTail)?;

ValueReference:
	LiteralValueReference | VariableValue | ComputationValue;

AnnotationValueReference returns ValueReference:
	ValueReference | ListValue
;

LiteralValueReference returns ValueReference:
	NumberValue | StringValue | BoolValue;

StringValue returns LiteralValueReference:
	{StringValue} value=STRING;

NumberValue returns LiteralValueReference:
    {NumberValue} value=XNumberLiteral
;

XNumberLiteral returns xbase::XNumberLiteral :
    {XNumberLiteral} value=Number;


BoolValue returns LiteralValueReference: 
    {BoolValue} value = XBooleanLiteral
;

XBooleanLiteral returns xbase::XBooleanLiteral:
	{XBooleanLiteral} ('false' | 'FALSE' | isTrue?='true' | isTrue?="TRUE")
;

VariableValue returns ValueReference:
	{VariableValue} value=VariableReference;

ListValue returns LiteralValueReference:
	{ListValue} '{' values+=ValueReference (',' values+=ValueReference)* '}';
	
ComputationValue returns ValueReference:
	AggregatedValue | FunctionEvaluationValue;
	
FunctionEvaluationValue returns ComputationValue:
	{FunctionEvaluationValue} 'eval' '(' expression=XExpression ')';
	
AggregatedValue returns ComputationValue:
    // TODO aggregator should be QualifiedName
 	{AggregatedValue} aggregator=[types::JvmDeclaredType|ValidID] call=PatternCall;