package org.eclipse.incquery.runtime.runonce.tests

import "http:///org/incquery/examples/library/1.0"

pattern bookAuthors(book, author) {
	Book.authors(book, author);
}

pattern booksWithMultipleAuthors(book) {
		Book(book);
		numberOfBooks == count find bookAuthors(book, _author);
		check(numberOfBooks > 1);
}

pattern sumOfPagesInLibrary(library, sumOfPages) {
	Library.sumOfPages(library, sumOfPages);
}

pattern singleAuthoredFirstBooks(library, firstBook) {
	Library.writers.firstBook(library, firstBook);
	1 == count find bookAuthors(firstBook, _author);
}

pattern longSciFiBooksOfAuthor(author, book) {
	Writer.scifiBooks(author, book);
	Book.pages(book, pages);
	check(pages > 100);
}

pattern requestCountOfLibrary(library, reqCount) {
	Library.requestCount(library, reqCount);
}

pattern someBooksWithTwoAuthors(library, book) {
	Library.requestCount(library, _c);
	Library.someBooks(library, book);
	2 == count find bookAuthors(book, _author);
}