package org.eclipse.viatra.query.runtime.runonce.tests

import "http://www.eclipse.org/viatra/examples/library/1.0"

pattern bookAuthors(book : Book, author : Writer) {
	Book.authors(book, author);
}

pattern booksWithMultipleAuthors(book : Book) {
		Book(book);
		numberOfBooks == count find bookAuthors(book, _author);
		check(numberOfBooks > 1);
}

pattern sumOfPagesInLibrary(library : Library, sumOfPages) {
	Library.sumOfPages(library, sumOfPages);
}

pattern singleAuthoredFirstBooks(library : Library, firstBook : Book) {
	Library.writers.firstBook(library, firstBook);
	1 == count find bookAuthors(firstBook, _author);
}

pattern longSciFiBooksOfAuthor(author : Writer, book : Book) {
	Writer.scifiBooks(author, book);
	Book.pages(book, pages);
	check(pages > 100);
}

pattern requestCountOfLibrary(library : Library, reqCount) {
	Library.requestCount(library, reqCount);
}

pattern someBooksWithTwoAuthors(library : Library, book : Book) {
	Library.requestCount(library, _c);
	Library.someBooks(library, book);
	2 == count find bookAuthors(book, _author);
}